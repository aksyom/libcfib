_bin = []
_lib = []
Import('*')
env = root_env.Clone()
env.Append(CPPDEFINES = '_WITH_SYSAPI_' + config_system_API)

nasm_env = env.Clone();
nasm_env.Append(BUILDERS = {'NASMObject' : Builder(action = 'nasm -f $NASM_FORMAT $NASM_FLAGS -o $TARGET $SOURCE')})
if config_system_ABI == 'sysv-amd64':
    nasm_suffix = '-sysv-amd64'
    nasm_env['NASM_FORMAT'] = 'elf64'
else:
    print "ERROR: Unsupported System ABI: " + config_system_ABI
    Exit(1)
nasm_shared_obj = nasm_env.NASMObject('cfib' + nasm_suffix + '.os', 'cfib' + nasm_suffix + '.asm', NASM_FLAGS='-D _ELF_SHARED=1')
nasm_static_obj = nasm_env.NASMObject('cfib' + nasm_suffix + '.o', 'cfib' + nasm_suffix + '.asm')
if config_have_c11_thread_local:
    env.Append(CPPDEFINES = '_WITH_C11_THREAD_LOCAL')
if config_have_c11_atomics:
    env.Append(CPPDEFINES = '_WITH_C11_ATOMICS')
env.Append(LIBPATH = ['.'])

lib_variants = [('', '')]
if config_have_c11_atomics:
    lib_variants += [('-D_PROFILED_BUILD', '_profiled')]
    have_profiled_libs = True
for cppdefs, suffix in lib_variants:
    e = env.Clone()
    e.Append(CPPDEFINES = [cppdefs])
    shared_objects = [e.SharedObject('cfib' + suffix, 'cfib.c'), nasm_shared_obj]
    static_objects = [e.StaticObject('cfib' + suffix, 'cfib.c'), nasm_static_obj]
    _lib += [e.SharedLibrary(target = 'cfib' + suffix, source = shared_objects)]
    _lib += [e.StaticLibrary(target = 'cfib' + suffix + '_static', source = static_objects)]

test_variants = [('', ''), ('-D_TEST_C11_BUILD', '_c11')]
for cppdefs, suffix in test_variants:
    e_shared = env.Clone()
    e_static = env.Clone()
    e_profiled = env.Clone()
    for e in (e_shared, e_static, e_profiled):
        e.Append(CPPDEFINES = [cppdefs]);
    e_shared.Append(LIBS = ['cfib'])
    e_static.Append(LIBS = ['cfib_static'])
    e_profiled.Append(LIBS = ['cfib_profiled'])
    test_obj = e.Object('test_cfib' + suffix, 'test_cfib.c')
    _bin += [e_shared.Program(target = 'test_cfib' + suffix, source = [test_obj])]
    _bin += [e_static.Program(target = 'test_cfib' + suffix + '_static', source = [test_obj])]
    _bin += [e_profiled.Program(target = 'test_cfib' + suffix + '_profiled', source = [test_obj])]

_ret = {'test_bin': _bin, 'lib': _lib}
Return('_ret')
